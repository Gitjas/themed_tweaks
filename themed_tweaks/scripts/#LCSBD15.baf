////////////////////////////////////////////////////////////////////////////////////////////////////
// Cut Scene Replacement for BDCut15                                                              //
// PC shouldn't be able to be privy to the fight between Hephernaan and Jayvis if still in combat //
// If out of combat, they can see him hitting Jayvis and                                          //
// the movement of the rest of the NPCs, but not make out what is being said                      //
////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		CreateCreature("bddarnas",[4895.50],SW)  // Darnas
		CreateCreature("bdbarghe",[4965.65],SW)  // The Barghest
		CreateCreature("bdolonei",[4920.90],SW)  // Oloneiros
		CreateCreature("bdcaelar",[4960.140],SW)  // Caelar Argent
		CreateCreature("bdvichan",[5015.155],SW)  // Vichand
		CreateCreature("bdhormor",[5025.95],SW)  // Hormorn
		CreateCreature("bdrhayla",[5080.155],SW)  // Rhayla
		CreateCreature("BDCCHOB1",[4655.275],S)  // Mercenary
		CreateCreature("BDCCSAR1",[4875.410],W)  // Crusader Sergeant
		CreateCreature("bdjayvis",[4675.460],NW)  // Jayvis
		CreateCreature("bdhepher",[4630.425],SE)  // Hephernaan
		ActionOverride("bddarnas",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdbarghe",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdolonei",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdvichan",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdhormor",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdrhayla",ChangeAIScript("bdcutsce",OVERRIDE))
		ChangeEnemyAlly("bdrhayla",NEUTRAL)  // Rhayla
		ActionOverride("BDCCHOB1",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("BDCCSAR1",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdjayvis",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("bdhepher",ChangeAIScript("bdcutsce",OVERRIDE))
		ActionOverride("BDCCHOB1",SetSequence(SEQ_HEAD_TURN))
		ActionOverride("BDCCSAR1",SetSequence(SEQ_HEAD_TURN))
		ActionOverride("bdcaelar",ChangeAIScript("",RACE))
		ActionOverride("bdcaelar",ChangeAIScript("",GENERAL))
		FadeToColor([10.0],0)
		SmallWait(4)
		CreateCreature("cutspy2",[4665.425],N)  // No such index
// SKIP 15
		ApplySpellRES("clearcld",Player1)  // No such index
		MoveViewPoint([4665.425],INSTANT)
		ActionOverride("bddarnas",JumpToPoint([4710.220]))
		ActionOverride("bdbarghe",JumpToPoint([4785.265]))
		ActionOverride("bdolonei",JumpToPoint([4735.285]))
		ActionOverride("bdcaelar",JumpToPoint([4740.350]))
		ActionOverride("bdvichan",JumpToPoint([4820.350]))
		ActionOverride("bdhormor",JumpToPoint([4835.300]))
		ActionOverride("bdrhayla",JumpToPoint([4890.360]))
		SmallWait(2)
		ActionOverride("bdhepher",FaceObject("bdcaelar"))  // Caelar Argent
		ActionOverride("bdjayvis",FaceObject("bdcaelar"))  // Caelar Argent
		FadeFromColor([10.0],0)
END

IF
	Dead("bdivann")  // Ivann
	Dead("bdcrus10")  // Barachus
	Dead("bdcrus11")  // Kerra Tinbasher
	Dead("bdcrus12")  // Seed
	Dead("bdcrus13")  // Elwood Dowser
	Dead("bdcrus14")  // Twelve-Fingers
THEN
	RESPONSE #100
		CutSceneId(Player1)

		SmallWait(10)
		ActionOverride("bdhepher",SetSequence(SEQ_ATTACK_SLASH))
		SmallWait(10)
		ActionOverride("bdjayvis",SetSequence(SEQ_DAMAGE))
		Wait(2)
		
		MoveViewObject(Player1,INSTANT)
		SmallWait(10)
		DisplayStringHead(Player1,@2000) // ~Well, seems we're not the only ones unhappy about the bridge blowing up.~
		Wait(3)
		
		MoveViewPoint([4665.425],INSTANT)
		SmallWait(4)
		ActionOverride("bddarnas",MoveToPoint([4710.220]))
		ActionOverride("bdbarghe",MoveToPoint([4785.265]))
		ActionOverride("bdolonei",MoveToPoint([4735.285]))
		ActionOverride("bdcaelar",MoveToPoint([4740.350]))
		ActionOverride("bdvichan",MoveToPoint([4820.350]))
		ActionOverride("bdhormor",MoveToPoint([4835.300]))
		ActionOverride("bdrhayla",MoveToPoint([4890.360]))
		ActionOverride("bdhepher",SetSequence(SEQ_ATTACK_SLASH))
		SmallWait(10)
		ActionOverride("bdjayvis",SetSequence(SEQ_DAMAGE))
		Wait(2)
END

IF
	Dead("bdivann")  // Ivann
	Dead("bdcrus10")  // Barachus
	Dead("bdcrus11")  // Kerra Tinbasher
	Dead("bdcrus12")  // Seed
	Dead("bdcrus13")  // Elwood Dowser
	Dead("bdcrus14")  // Twelve-Fingers
	IsValidForPartyDialogue(Player2)
THEN
	RESPONSE #100
		CutSceneId(Player1)

		MoveViewObject(Player2,INSTANT)
		SmallWait(10)
		DisplayStringHead(Player2,@2001)	// ~Why'd they have it rigged if they weren't supposed to light it?~
		Wait(5)
END

IF
	Dead("bdivann")  // Ivann
	Dead("bdcrus10")  // Barachus
	Dead("bdcrus11")  // Kerra Tinbasher
	Dead("bdcrus12")  // Seed
	Dead("bdcrus13")  // Elwood Dowser
	Dead("bdcrus14")  // Twelve-Fingers
	!IsValidForPartyDialogue(Player2)
	IsValidForPartyDialogue(Player3)
THEN
	RESPONSE #100
		CutSceneId(Player1)

		MoveViewObject(Player3,INSTANT)
		SmallWait(10)
		DisplayStringHead(Player3,@2001)	// ~Why'd they have it rigged if they weren't supposed to light it?~
		Wait(5)		
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		
		MoveViewObject(Player1,INSTANT)
		DisplayStringHead(Player1,@2002)	// ~Wish I could hear ... ~
		Wait(4)
		
		MoveViewPoint([4665.425],INSTANT)
		SmallWait(4)
		ActionOverride("bdhepher",FaceObject("bdcaelar"))  // Caelar Argent
		ActionOverride("bdjayvis",FaceObject("bdcaelar"))  // Caelar Argent
		SmallWait(20)

		StartMusic(MISC3,QUICK_FADE)
		SetGlobal("bd_plot","global",161)
		SmallWait(4)
		StartCutSceneEx("bdcut16",TRUE)
END