///////////////////////////////////
// Changes to bg1000 area script //
///////////////////////////////////

IF
	Global("BD_Plot","GLOBAL",100)
	GlobalGT("#L_CWBridgeQuest","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("BD_Plot","GLOBAL",101) // Skip initial conversation with Corwin if we ignored her when we first got to camp
		Continue()
END

IF
	GlobalLT("#L_CWBridgeQuest","GLOBAL",3)
	OR(2)
		TriggerOverride(Player1,See("BDCRUS10"))
		TriggerOverride(Player1,See("BDCRUS11"))		
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("#L_CWBridgeQuest","GLOBAL",3)
		AddJournalEntry(@3002,QUEST)
	    SetInterrupt(TRUE)
END

IF
	Global("BD_Plot","GLOBAL",156)
	GlobalLT("#L_CWBridgeQuest","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("#L_CWBridgeQuest","GLOBAL",97)
		AddJournalEntry(@3012,QUEST_DONE)
END

IF
	Global("BD_Plot","GLOBAL",156)
	Global("#L_CWBridgeQuest","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("#L_CWBridgeQuest","GLOBAL",98)
		AddJournalEntry(@3013,QUEST_DONE)
END

IF
	Global("BD_Plot","GLOBAL",156)
	Global("#L_CWBridgeQuest","GLOBAL",6)
THEN
	RESPONSE #100
		SetGlobal("#L_CWBridgeQuest","GLOBAL",99)
		AddJournalEntry(@3014,QUEST_DONE)
END

IF
	IsValidForPartyDialogue("CORWIN")
	OR(2) 
		Global("#L_CWBridgeQuest","GLOBAL",2)
		Global("#L_CWBridgeQuest","GLOBAL",3)
THEN
	RESPONSE #100
		ActionOverride("CORWIN",StartDialogueNoSet(Player1))
END

IF
	!InParty("CORWIN")
	GlobalLT("#L_CWBridgeQuest","GLOBAL",4)
	OR(3)
		GlobalTimerExpired("#L_CWBridgeScoutTimer","MYAREA") 
		Global("#L_CWBridgeQuest","GLOBAL",2)
		Global("#L_CWBridgeQuest","GLOBAL",3)
	TriggerOverride("CORWIN",See(Player1))
THEN
	RESPONSE #100
		ActionOverride("CORWIN",StartDialogueNoSet(Player1))
END

// If the plan includes the PC being a wookiee (fake prisoner)
// There will be 3 FF: two front, one back
If
	Global("#L_CWBridgeQuest","GLOBAL",6)
	Global("#L_CWBridgeFFSet","GLOBAL",0)
THEN
	RESPONSE #100
		CreateCreature("#LFFAss1",[2840.1450],NE)
		CreateCreature("#LFFAss2",[2895.1505],NE)
		CreateCreature("#LFFAss3",[2770.1525],NE)
		ChangeEnemyAlly("#LFFAss1",ALLY)
		ChangeEnemyAlly("#LFFAss2",ALLY)
		ChangeEnemyAlly("#LFFAss3",ALLY)
		Continue()
END

// If the plan doesn't include the wookiee
// There will be only the 2 front FF
If
	Global("#L_CWBridgeQuest","GLOBAL",5)
	Global("#L_CWBridgeFFSet","GLOBAL",0)
THEN
	RESPONSE #100
		CreateCreature("#LFFAss1",[2840.1450],NE)
		CreateCreature("#LFFAss2",[2895.1505],NE)
		ChangeEnemyAlly("#LFFAss1",ALLY)
		ChangeEnemyAlly("#LFFAss2",ALLY)
		Continue()
END

If
	Global("#L_CWBridgeFFSet","GLOBAL",0)
	OR(2)
		Global("#L_CWBridgeQuest","GLOBAL",5)
		Global("#L_CWBridgeQuest","GLOBAL",6)
THEN
	RESPONSE #100
		SetGlobal("#L_CWBridgeFFSet","GLOBAL",1)
		CreateCreature("bdfistex",[2400.1615],NE)
		CreateCreature("bdfistex",[2455.1650],NE)
		CreateCreature("bdfistex",[2340.1640],NE)
		CreateCreature("bdfistex",[2395.1675],NE)

		CreateCreature("bdfistex",[2280.1665],NE)
		CreateCreature("bdfistex",[2335.1700],NE)
		CreateCreature("bdfistex",[2220.1690],NE)
		CreateCreature("bdfistex",[2275.1725],NE)
		
		CreateCreature("bdfistex",[2160.1715],NE)
		CreateCreature("bdfistex",[2215.1750],NE)
		CreateCreature("bdfistex",[2100.1740],NE)
		CreateCreature("bdfistex",[2155.1775],NE)
		
		CreateCreature("bdfistex",[2040.1765],NE)
		CreateCreature("bdfistex",[2095.1800],NE)
		CreateCreature("bdfistex",[1980.1790],NE)
		CreateCreature("bdfistex",[2035.1825],NE)

		Continue()
END