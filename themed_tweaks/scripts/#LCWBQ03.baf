//////////////////////////////////////////////////////////
// Coast Way Bridge Quest Cut Scene                     //
// The futile attempt to clear the bridge of explosives //
//////////////////////////////////////////////////////////

// Just 2 FF and PC walk to bridge
IF
	Global("#L_CWBridgeQuest","GLOBAL",5)
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")

		Explore()
		ActionOverride("#LFFAss2",FollowObjectFormation("#LFFAss1",1,2))
		ActionOverride(Player1,FollowObjectFormation("#LFFAss1",1,3))
		MoveToPoint([3840.1010])
		MoveViewPoint([3840.1010],VERY_FAST)
		Wait(2)

		MoveViewObject("BDCRUS13",INSTANT)
		DisplayStringHead("BDCRUS13",#%KNOW_YOU_NOT%)
		Wait(2)

		MoveViewObject("#LFFAss1",INSTANT)
		DisplayStringHead("#LFFAss1",@2120) // ~We're returning from Baldur's Gate.~
		Wait(2)
		
		MoveViewObject("BDCRUS13",INSTANT)
		DisplayStringHead("BDCRUS13",@2122) // ~I don't recognize ya.  What's the password?~
		Wait(2)

		Continue()
END	

// 3 FF and PC walk to bridge, 2 FF in fron, PC next, then last FF
IF
	Global("#L_CWBridgeQuest","GLOBAL",6)
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")

		Explore()
		ActionOverride("#LFFAss2",FollowObjectFormation("#LFFAss1",1,2))
		ActionOverride(Player1,FollowObjectFormation("#LFFAss1",1,3))
		ActionOverride("#LFFAss3",FollowObjectFormation("#LFFAss1",1,4))
		MoveToPoint([3840.1010])
		MoveViewPoint([3840.1010],VERY_FAST)
		Wait(2)

		MoveViewObject("BDCRUS13",INSTANT)
		DisplayStringHead("BDCRUS13",#%KNOW_YOU_NOT%)
		Wait(2)

		MoveViewObject("#LFFAss1",INSTANT)
		DisplayStringHead("#LFFAss1",@2121) // ~We're returning from Baldur's Gate with a prisoner.~
		Wait(2)
		
		MoveViewObject("BDCRUS13",INSTANT)
		DisplayStringHead("BDCRUS13",@2122) // ~I don't recognize ya.  What's the password?~
		Wait(2)

		Continue()
END

// If no attempt to get the password
// The two front fist will fail in their bluff
IF
	Global("#L_CWBridgePassword","GLOBAL",0)
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")
		
		DisplayStringHead("#LFFAss1",@2123) // ~Uh...password?  Uh, we don't need no stinkin' password!~
		Wait(1)
		
		DisplayStringHead("BDCRUS13",@2124) // ~Oh yes you do!  Hey!  That's the Hero!! And these aren't no stickin' elites!~
		Wait(1)
		
		Continue()
END

// If a password was obtained
// the informant will escape and alert the rest of the crusader guards
IF
	Global("#L_CWBridgePassword","GLOBAL",1)
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")
		
		DisplayStringHead("#LFFAss1",@2124) // ~It's nikto.~
		Wait(1)
		
		DisplayStringHead("BDCRUS13",@2125) // ~Alright, that's it alright.~
		Wait(1)
		
		Continue()
END

IF
	Global("#L_CWBridgePassword","GLOBAL",1)
	Global("#L_CWBridgeQuest","GLOBAL",5) // No prisoner
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")
		
		// This will be a custom NPC soon
		DisplayStringHead("BDCRUS13",@2126) // ~WAIT!  WAIT!  Don't let 'em pass!  They're Flaming Fist and that's the Hero with 'em!~
		Wait(1)
		
		Continue()
END

IF
	Global("#L_CWBridgePassword","GLOBAL",1)
	Global("#L_CWBridgeQuest","GLOBAL",6) // No prisoner
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")
		
		// This will be a custom NPC soon
		DisplayStringHead("BDCRUS13",@2127) // ~WAIT!  WAIT!  Don't let 'em pass!  That's the Flaming Fist with the Hero!~
		Wait(1)
		
		Continue()
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("#LFFAss1")
		DisplayStringHead(Player1,@2128) // ~Oh great, how'd he get loose?~
		Wait(1)
		ClearAllActions()
		SetGlobal("bd_plot","global",156)
		StartCutSceneEx("bdcut14",TRUE)
END