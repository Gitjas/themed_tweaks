/////////////////////////////////////////////////////////////////////////
// Optional component that allows you to roam Baldur's Gate unescorted //
/////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sod_ditch_corwin BEGIN
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/sod_ditch_corwin.d~ USING ~%tra_loc%/%s/sod_ditch_corwin.tra~

	// Go through all the areas where corwin is an 'actor' and have her destroy herself
	// if the player elects to wander the streets of Baldur's Gate alone
	ACTION_DEFINE_ARRAY area_list BEGIN
		bd0010
		bd0020
		bd0030
   		bd0040
   		bd0050
	END

	OUTER_FOR (area_num = 0; VARIABLE_IS_SET $area_list(~%area_num%~); ++area_num) BEGIN 
		OUTER_SPRINT area_name $area_list("%area_num%")
		LAF REPLACE_SCRIPT_BLOCK
			INT_VAR 
				only_once = 1
			STR_VAR 
				script = EVAL ~%area_name%~
				match = ~ActionOverride("bdschael",JumpToObject(Player1))~
				insert = ~%mod_root%/scripts/sod_ditch_corwin1.baf~
		END
		EXTEND_TOP ~%area_name%.bcs~ ~%mod_root%/scripts/sod_ditch_corwin2.baf~
		COPY_EXISTING ~%area_name%.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("bd_corwin_special2","%area_name%",0)~ ~Global("bd_corwin_special2","%area_name%",0) Global("#L_NoCorwin","GLOBAL",0)~
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!GlobalTimerNotExpired("BD_Corwin_OT","%area_name%")~ ~!GlobalTimerNotExpired("BD_Corwin_OT","%area_name%") Global("#L_NoCorwin","GLOBAL",0)~
			END
	END
	
	EXTEND_TOP ~bd0104.bcs~ ~%mod_root%/scripts/sod_ditch_corwin2.baf~
	EXTEND_TOP ~bd0109.bcs~ ~%mod_root%/scripts/sod_ditch_corwin2.baf~
	EXTEND_TOP ~bd0111.bcs~ ~%mod_root%/scripts/sod_ditch_corwin2.baf~
	EXTEND_TOP ~bd0121.bcs~ ~%mod_root%/scripts/sod_ditch_corwin2.baf~

	// Hooks to allow for the conspiracy quest and drink scene if not done before
	EXTEND_TOP ~bd0106.bcs~ ~%mod_root%/scripts/sod_ditch_corwin_bd0106.baf~
END
