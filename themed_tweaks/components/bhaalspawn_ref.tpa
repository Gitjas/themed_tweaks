//////////////////////////////////////////////////////////////
// Weeds out errant references to the PC being a bhaalspawn //
//////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bhaalspawn_ref BEGIN
	// There are two times when group members as a whole may become aware of the PC heritage
	// 1-when the note from Gorion is found (BG1NPC reads it aloud for all to hear)
	// 2-when the PC talks to Tamoko
	
	ACTION_IF (MOD_IS_INSTALLED ~bg1npc.tp2~ 0) OR (MOD_IS_INSTALLED ~Setup-AjantisBG1.tp2~ 0) BEGIN
		// When the note is found, a global is set so that no matter if the NPC was there or not, they automatically know
		// I've changed that to a local so they have to be present to know
		COPY_EXISTING ~%AJANTIS_BCS%.bcs~ ~override~
					~%BRANWEN_BCS%.bcs~ ~override~
					~%DYNAHEIR_BCS%.bcs~ ~override~
					~%JAHEIRA_BCS%.bcs~ ~override~
					~%KIVAN_BCS%.bcs~ ~override~
					~%TIAX_BCS%.bcs~ ~override~
					~%XAN_BCS%.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("X#HalfBrotherRevealed","GLOBAL"~ ~Global("#L_BSRefSarvSib","LOCALS"~  //Only found in Kivan.bcs
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("X#AjTamokoKnowBhaal","GLOBAL",1)~ ~FALSE()~							  //Only found in Ajantis.bcs
			END
		BUT_ONLY
	
		ACTION_IF (FILE_EXISTS_IN_GAME ~AJANTD.bcs~) BEGIN
			COPY_EXISTING ~AJANTD.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				END
			BUT_ONLY
		END
		ACTION_IF (FILE_EXISTS_IN_GAME ~corand.bcs~) BEGIN
			COPY_EXISTING ~corand.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				END
			BUT_ONLY
		END
		ACTION_IF (FILE_EXISTS_IN_GAME ~xand.bcs~) BEGIN
			COPY_EXISTING ~xand.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				END
			BUT_ONLY
		END

		OUTER_SET STATE_DYN_BXAN = ~-1~
		OUTER_SET STATE_DYN_BXZAR = ~-1~
		ACTION_IF (MOD_IS_INSTALLED ~bg1npc.tp2~ 0) BEGIN
			// Load variables with BG1NPC dialogue states that need to be modified
			OUTER_SET STATE_DYN_BXAN = STATE_WHICH_SAYS 1121 IN ~bg1npc\tra\english\x#xanlt.tra~ FROM ~%DYNAHEIR_BANTER%~
			OUTER_SET STATE_DYN_BXZAR = STATE_WHICH_SAYS 320 IN ~bg1npc\tra\english\x#dynah.tra~ FROM ~%DYNAHEIR_BANTER%~
		END
		
		// Load variables with dialogue changes
		OUTER_SPRINT DIALOG_DYN_BXAN ~~
		ACTION_IF (STATE_DYN_BXAN > ~-1~) BEGIN
			OUTER_SPRINT DIALOG_DYN_BXAN ~~~~~ADD_STATE_TRIGGER %DYNAHEIR_BANTER% %STATE_DYN_BXAN% ~TriggerOverride("XAN",Global("#L_BSRefKnows","LOCALS",1))~ ~~~~~
		END
		OUTER_SPRINT DIALOG_DYN_BXZAR ~~
		ACTION_IF (STATE_DYN_BXZAR > ~-1~) BEGIN
			OUTER_SPRINT DIALOG_DYN_BXZAR ~~~~~ADD_STATE_TRIGGER %DYNAHEIR_BANTER% %STATE_DYN_BXZAR% ~TriggerOverride("XZAR",Global("#L_BSRefKnows","LOCALS",1))~ ~~~~~
		END
		OUTER_SPRINT DIAL_AMELIA ~~
		ACTION_IF (FILE_EXISTS_IN_GAME ~X#AMELIA.DLG~) BEGIN
			OUTER_SPRINT DIAL_AMELIA ~~~~~REPLACE_TRIGGER_TEXT X#AMELIA ~!Global("P#KnowBhaal","GLOBAL",1)~ ~TriggerOverride(Player1,Global("#L_BSRefKnows","LOCALS",0))~
REPLACE_TRIGGER_TEXT X#AMELIA ~Global("P#KnowBhaal","GLOBAL",1)~ ~TriggerOverride(Player1,Global("#L_BSRefKnows","LOCALS",1))~
ALTER_TRANS X#AMELIA BEGIN 2 END BEGIN 0 END BEGIN "ACTION" ~ActionOverride(Player1,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player2,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player3,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player4,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player5,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player6,SetGlobal("#L_BSRefKnows","LOCALS",1))~ END~~~~~
		END
		OUTER_SPRINT DIAL_GORLET ~~
		ACTION_IF (FILE_EXISTS_IN_GAME ~X#GORLET~) BEGIN
			OUTER_SPRINT DIAL_GORLET ~~~~~REPLACE_ACTION_TEXT X#GORLET ~SetGlobal("P#KnowBhaal","GLOBAL",1)~ ~ActionOverride(Player1,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player2,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player3,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player4,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player5,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player6,SetGlobal("#L_BSRefKnows","LOCALS",1))~ ~~~~~
		END
	END
		
	// Compile dialogues
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/bhaalspawn_ref.d~ USING ~%tra_loc%/%s/bhaalspawn_ref.tra~
END
