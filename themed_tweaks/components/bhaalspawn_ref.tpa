//////////////////////////////////////////////////////////////
// Weeds out errant references to the PC being a bhaalspawn //
//////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bhaalspawn_ref BEGIN
	// There are two times when group members as a whole may become aware of the PC heritage
	// 1-when the note from Gorion is found (BG1NPC reads it aloud for all to hear)
	// 2-when the PC talks to Tamoko

	// Dorn's stock bhaalspawn chatter that needs to be controlled
	COPY_EXISTING ~%DORN_BCS%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("SPAWN_REVEAL","GLOBAL",0)~ ~Global("SPAWN_REVEAL","GLOBAL",0) Global("#L_BSRefKnows","LOCALS",1)~
		END
	BUT_ONLY
	
	// Dialogue changes that can only be compiled depending upon which mods are installed
	OUTER_SPRINT DIALOG_DYN_BXAN ~~
	OUTER_SPRINT DIALOG_DYN_BXZAR ~~
	OUTER_SPRINT DIALOG_JAH_BHAAL ~~
	OUTER_SPRINT DIALOG_JAH_LETTER ~~
	OUTER_SPRINT DIALOG_AMELIA ~~
	OUTER_SPRINT DIALOG_GORLET ~~
	OUTER_SPRINT REPLY_READ_ALOUD ~~~~~IF ~~ THEN REPLY @2003 /* ~*Read the note out loud to your group*~ */ GOTO READ_ALOUD_NO_JAHEIRA~~~~~
	OUTER_SPRINT REPLY_GOTO_1 ~~
	OUTER_SPRINT REPLY_GOTO_2 ~~

	ACTION_IF (MOD_IS_INSTALLED ~bg1npc.tp2~ 0) OR (MOD_IS_INSTALLED ~Setup-AjantisBG1.tp2~ 0) BEGIN
		// When the note is found, a global is set so that no matter if the NPC was there or not, they automatically know
		// I've changed that to a local so they have to be present to know
		COPY_EXISTING ~%AJANTIS_BCS%.bcs~ ~override~
					~%BRANWEN_BCS%.bcs~ ~override~
					~%DYNAHEIR_BCS%.bcs~ ~override~
					~%KIVAN_BCS%.bcs~ ~override~
					~%TIAX_BCS%.bcs~ ~override~
					~%XAN_BCS%.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("X#HalfBrotherRevealed","GLOBAL"~ ~Global("#L_BSRefSarvSib","LOCALS"~  //Only found in Kivan.bcs
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("X#AjTamokoKnowBhaal","GLOBAL",1)~ ~FALSE()~							  //Only found in Ajantis.bcs
			END
		BUT_ONLY
	
		COPY_EXISTING ~%AJANTIS_BCS%.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("X#AjantisLoveTalk","GLOBAL",29)~ ~Global("X#AjantisLoveTalk","GLOBAL",29) OR(2) !Global("X#AjantisLoveTalk","GLOBAL",29) TriggerOverride(Player1,Global("#L_BSRefKnows","LOCALS",0))~
			END
		BUT_ONLY
		
		COPY_EXISTING ~%JAHEIRA_BCS%.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL",1)~ ~Global("#L_BSRefKnows","LOCALS",0) NextTriggerObject(Player1) !Global("#L_BSRefKnows","LOCALS",0)~
			END
		BUT_ONLY
		
		COPY_EXISTING ~%YESLICK_BCS%.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("Chapter","GLOBAL",7)~ ~Global("Chapter","GLOBAL",7) Global("#L_BSRefKnows","LOCALS",1)~
			END
		BUT_ONLY
				
		ACTION_IF (FILE_EXISTS_IN_GAME ~AJANTD.bcs~) BEGIN
			COPY_EXISTING ~AJANTD.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				END
			BUT_ONLY
		END
		ACTION_IF (FILE_EXISTS_IN_GAME ~corand.bcs~) BEGIN
			COPY_EXISTING ~corand.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				END
			BUT_ONLY
		END
		ACTION_IF (FILE_EXISTS_IN_GAME ~xand.bcs~) BEGIN
			COPY_EXISTING ~xand.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("P#KnowBhaal","GLOBAL"~ ~Global("#L_BSRefKnows","LOCALS"~
				END
			BUT_ONLY
		END

		OUTER_SET STATE_AJA_FLIP_1 = ~-1~
		OUTER_SET STATE_AJA_FLIP_2 = ~-1~
		OUTER_SET STATE_DYN_BXAN = ~-1~
		OUTER_SET STATE_DYN_BXZAR = ~-1~
		OUTER_SET STATE_JAH_BHAAL = ~-1~
		OUTER_SET STATE_JAH_LETTER1 = ~-1~
		OUTER_SET STATE_JAH_LETTER2 = ~-1~
		ACTION_IF (MOD_IS_INSTALLED ~bg1npc.tp2~ 0) BEGIN
			// Load variables with BG1NPC dialogue states that need to be modified
			OUTER_SET STATE_AJA_FLIP_1 = STATE_WHICH_SAYS 145 IN ~bg1npc\tra\english\x#ajint.tra~ FROM ~%AJANTIS_JOINED%~
			OUTER_SET STATE_AJA_FLIP_2 = STATE_WHICH_SAYS 146 IN ~bg1npc\tra\english\x#ajint.tra~ FROM ~%AJANTIS_JOINED%~
			OUTER_SET STATE_DYN_BXAN = STATE_WHICH_SAYS 1121 IN ~bg1npc\tra\english\x#xanlt.tra~ FROM ~%DYNAHEIR_BANTER%~
			OUTER_SET STATE_DYN_BXZAR = STATE_WHICH_SAYS 320 IN ~bg1npc\tra\english\x#dynah.tra~ FROM ~%DYNAHEIR_BANTER%~
			OUTER_SET STATE_JAH_BHAAL = STATE_WHICH_SAYS 21 IN ~bg1npc\tra\english\x#jaheira.tra~ FROM ~%JAHEIRA_JOINED%~
			OUTER_SET STATE_JAH_LETTER1 = STATE_WHICH_SAYS 25 IN ~bg1npc\tra\english\x#jaheira.tra~ FROM ~%JAHEIRA_JOINED%~
			OUTER_SET STATE_JAH_LETTER2 = STATE_WHICH_SAYS 41 IN ~bg1npc\tra\english\x#jaheira.tra~ FROM ~%JAHEIRA_JOINED%~
		END
		ACTION_IF (STATE_AJA_FLIP_1 = ~-1~) BEGIN
			OUTER_SET STATE_AJA_FLIP_1 = STATE_WHICH_SAYS 145 IN ~ajantisbg1\Translations\English\AJANTISBHAALLTS_RA1.TRA~ FROM ~%AJANTIS_JOINED%~
			OUTER_SET STATE_AJA_FLIP_2 = %STATE_AJA_FLIP_1%
		END
		
		// Load variables with dialogue changes
		ACTION_IF (STATE_DYN_BXAN > ~-1~) BEGIN
			OUTER_SPRINT DIALOG_DYN_BXAN ~~~~~ADD_STATE_TRIGGER %DYNAHEIR_BANTER% %STATE_DYN_BXAN% ~TriggerOverride("XAN",Global("#L_BSRefKnows","LOCALS",1))~ ~~~~~
		END
		ACTION_IF (STATE_DYN_BXZAR > ~-1~) BEGIN
			OUTER_SPRINT DIALOG_DYN_BXZAR ~~~~~ADD_STATE_TRIGGER %DYNAHEIR_BANTER% %STATE_DYN_BXZAR% ~TriggerOverride("XZAR",Global("#L_BSRefKnows","LOCALS",1))~ ~~~~~
		END
		ACTION_IF (STATE_JAH_BHAAL > ~-1~) BEGIN
			OUTER_SPRINT DIALOG_JAH_BHAAL ~~~~~ADD_TRANS_ACTION %JAHEIRA_JOINED% BEGIN %STATE_JAH_BHAAL% END BEGIN 0 2 END ~ActionOverride(Player2,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player3,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player4,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player5,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player6,SetGlobal("#L_BSRefKnows","LOCALS",1))~ ~~~~~
		END
		ACTION_IF (STATE_JAH_LETTER1 > ~-1~) AND (STATE_JAH_LETTER2 > ~-1~)BEGIN
			OUTER_SPRINT REPLY_READ_ALOUD ~~~~~IF ~IsValidForPartyDialogue("jaheira")~ THEN REPLY @2003 /* ~*Read the note out loud to your group*~ */ GOTO READ_ALOUD_JAHEIRA
			IF ~!IsValidForPartyDialogue("jaheira")~ THEN REPLY @2003 /* ~*Read the note out loud to your group*~ */ GOTO READ_ALOUD_NO_JAHEIRA~~~~~

			OUTER_SPRINT REPLY_GOTO_1 ~~~~~ADD_STATE_TRIGGER %JAHEIRA_JOINED% %STATE_JAH_LETTER1% ~Global("#L_BSRefLetter","GLOBAL",1)~ SET_WEIGHT %JAHEIRA_JOINED% %STATE_JAH_LETTER1% #-2 ~~~~~  
			OUTER_SPRINT REPLY_GOTO_2 ~~~~~ADD_STATE_TRIGGER %JAHEIRA_JOINED% %STATE_JAH_LETTER2% ~Global("#L_BSRefLetter","GLOBAL",2)~ SET_WEIGHT %JAHEIRA_JOINED% %STATE_JAH_LETTER2% #-1 ~~~~~   
			OUTER_SPRINT DIALOG_JAH_LETTER ~~~~~IF ~~ THEN BEGIN READ_ALOUD_JAHEIRA
				SAY @2004 // ~Hello <CHARNAME>, If you are reading this, it means I have met an untimely death...	
				IF ~~ THEN REPLY @2005 /* ~Jaheira, did you not know about this?~ */ DO ~SetGlobal("X#GorionTalkingLetter","GLOBAL",4) ActionOverride(Player1,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player2,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player3,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player4,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player5,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player6,SetGlobal("#L_BSRefKnows","LOCALS",1)) SetGlobal("X#JaFriendTalk","GLOBAL",6) SetGlobal("#L_BSRefLetter","GLOBAL",1) ActionOverride("Jaheira",StartDialogueNoSet(Player1))~ EXIT
				IF ~~ THEN REPLY @2006 /* ~You... you knew, didn't you? You knew about this and would not speak! You... you betrayer!~ */ DO ~SetGlobal("X#GorionTalkingLetter","GLOBAL",4) ActionOverride(Player1,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player2,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player3,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player4,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player5,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player6,SetGlobal("#L_BSRefKnows","LOCALS",1)) SetGlobal("X#JaFriendTalk","GLOBAL",6) SetGlobal("#L_BSRefLetter","GLOBAL",2) ActionOverride("Jaheira",StartDialogueNoSet(Player1))~ EXIT 
			END ~~~~~
		END
		ACTION_IF (FILE_EXISTS_IN_GAME ~X#AMELIA.DLG~) BEGIN
			OUTER_SPRINT DIALOG_AMELIA ~~~~~REPLACE_TRIGGER_TEXT X#AMELIA ~!Global("P#KnowBhaal","GLOBAL",1)~ ~NextTriggerObject(Player1) !Global("#L_BSRefKnows","LOCALS",1)~
REPLACE_TRIGGER_TEXT X#AMELIA ~Global("P#KnowBhaal","GLOBAL",1)~ ~NextTriggerObject(Player1) !Global("#L_BSRefKnows","LOCALS",0)~
ALTER_TRANS X#AMELIA BEGIN 2 END BEGIN 0 END BEGIN "ACTION" ~ActionOverride(Player1,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player2,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player3,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player4,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player5,SetGlobal("#L_BSRefKnows","LOCALS",1)) ActionOverride(Player6,SetGlobal("#L_BSRefKnows","LOCALS",1))~ END~~~~~
		END
		
		// Compile new changes for AJANTIS
		EXTEND_TOP ~%AJANTIS_BCS%.bcs~ ~%mod_root%/scripts/bhaalspawn_ref_ajantis.baf~
		
		// New dialogue for Ajantis so he knows about PC heritage
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/bhaalspawn_ref_ajantis_add.d~ USING ~%tra_loc%/%s/bhaalspawn_ref.tra~
	END

	ACTION_IF (FILE_EXISTS_IN_GAME ~X#GORLET.DLG~) BEGIN
		// Delete the letter-reading dialogue used by BG1NPC - it's just easier to start from scratch
		// It's not completely necessary to delete it first, but oh well
		DELETE ~override\X#GORLET.DLG~
		DELETE ~override\X#GORLET.BCS~
		
		// Provide a new override script
		LAF UPDATE_BCS
			INT_VAR
				action_taken = 1		// 0 = APPEND_FILE TEXT, 1 = EXTEND_TOP, 2 = EXTEND_BOTTOM
			STR_VAR
				baf_location = EVAL ~%mod_root%/scripts/~				// like ~mymod/bafs/~
				baf_name = ~bhaalspawn_ref_letter_script~				// like my_new_BG0146
				bcs_name = ~X#GORLET~									// like BG0146
		END
		
		// Until BG1NPC is fixed, we have to spawn the letter-reader in multiple places ourselves
		// Remove it from the 5th floor of the library
		LAF DELETE_SCRIPT_BLOCK
			INT_VAR
				only_once = 0
			STR_VAR
				script = EVAL ~%Candlekeep_Library_L5%~
				match = ~X#GorionTalkingLetter~
			RET old_ver = found
		END

		// Add it to Baldur.bcs if we're dealing with the old version
		ACTION_IF old_ver = 1 BEGIN
			EXTEND_TOP ~BALDUR.BCS~ ~%mod_root%/scripts/bhaalspawn_ref_letter_spawn.baf~
		END
	END ELSE BEGIN
		// Make sure the letter-reading cre is in the game for us to use and set up its spawning
		COPY ~%mod_root%/objects/X#GORLET.CRE~ ~override~
		// Provide a new override script
		LAF UPDATE_BCS
			INT_VAR
				action_taken = 1		// 0 = APPEND_FILE TEXT, 1 = EXTEND_TOP, 2 = EXTEND_BOTTOM
			STR_VAR
				baf_location = EVAL ~%mod_root%/scripts/~				// like ~mymod/bafs/~
				baf_name = ~bhaalspawn_ref_letter_script~				// like my_new_BG0146
				bcs_name = ~X#GORLET~									// like BG0146
		END
		// Spawn it whereever they first obtain Gorion's scroll
		EXTEND_TOP ~BALDUR.BCS~ ~%mod_root%/scripts/bhaalspawn_ref_letter_spawn.baf~
	END	
	
	// Compile dialogues
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/bhaalspawn_ref.d~ USING ~%tra_loc%/%s/bhaalspawn_ref.tra~
END
