//////////////////////////////////////////////
// Remove cut scenes not viewable by the PC //
//////////////////////////////////////////////


DEFINE_ACTION_FUNCTION cut_scenes BEGIN

	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		// Set required variables based on installed game
		ACTION_IF GAME_IS ~eet~ BEGIN
			OUTER_SET SOD_JOURNAL_01 = 259633
		END ELSE BEGIN
			OUTER_SET SOD_JOURNAL_01 = 59633
		END

		// Remove cut scenes that are started via dialogues
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/sod_cut_scenes.d~

		// Take out the scene with Irenicus, Caelar and Hephernaan
		COPY_EXISTING ~BDCUT10.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~FadeToColor([0.0],0)~ ~~
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~StartCutSceneEx("bdcut11",TRUE)~ ~MultiPlayerSync() FadeFromColor([20.0],0) EndCutSceneMode()~
			END
			
		// Take out the scene with Hephernaan and Jayvis at the Coast Way bridge
		// TODO: Future version, if not in combat, make it so can overhear Hephernaan yelling (just the one line) and see the scuffle...add group comments 
		COPY_EXISTING ~BD1000.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~StartCutSceneEx("bdcut15",TRUE)~ ~StartCutSceneEx("#LCSBD15",TRUE)~
			END
		
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LCSBD15.baf~
	END
END
