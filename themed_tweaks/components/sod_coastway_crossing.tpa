////////////////////////////////////////////////////
// Optional quest to scout the Coast Way Crossing //
////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sod_coastway_crossing BEGIN
	// Set some strrefs to be used in dialogues depending up on the installed game
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET BRIDGE_HELD_1 = 252899
		OUTER_SET BRIDGE_HELD_2 = 252903
	END ELSE BEGIN
		OUTER_SET BRIDGE_HELD_1 = 52899
		OUTER_SET BRIDGE_HELD_2 = 52903
	END
	
	// Disguised Flaming Fist
	// TODO: Make them externally exactly like the elites that attacked the palace BDGASS5
	//	  	 Have the Veteran call the PC into their place and then walk toward the bridge
	//       Override default area script to only fire when on quest cue
	//       There are a lot of changes to be made to the dialogue with Caelar
	COPY_EXISTING ~bdfist26.CRE~ ~override/#LFFAss1.cre~
		SAY NAME1 @1004	/* ~Disguised Flaming Fist Veteran~ */
		SAY NAME2 @1004	/* ~Disguised Flaming Fist Veteran~ */
		WRITE_LONG 0x0028 25344 // Animation = THIEF_MALE_HUMAN
		WRITE_BYTE 0x002c 226 // Metal color = Metal normal - Eigengrau
		WRITE_BYTE 0x002d 242 // Minor color = Thief Minor - Pale Olive
		WRITE_BYTE 0x002e 241 // Major color = Thief Major - Olive
		WRITE_BYTE 0x0030 228 // Leather color = Leather - Spectral Green
		WRITE_BYTE 0x0031 244 // Armor color = Fighter Minor - Dark Cadet Gray
		WRITE_ASCII SCRIPT_OVERRIDE ~#LFFAss1~ #8
		WRITE_ASCII SCRIPT_GENERAL ~~ #8
		WRITE_ASCII SCRIPT_RACE ~~ #8
		WRITE_ASCII SCRIPT_CLASS ~~ #8
		WRITE_ASCII SCRIPT_DEFAULT ~~ #8
		WRITE_ASCII DEATHVAR ~#LFFAss1~ #32
		WRITE_ASCII DIALOG ~#LFFAss1~ #8

	COPY_EXISTING ~bdfistm2.CRE~ ~override/#LFFAss2.cre~
		SAY NAME1 @1005 /* ~Disguised Flaming Fist Battlemage~ */
		SAY NAME2 @1005	/* ~Disguised Flaming Fist Battlemage~ */
		WRITE_LONG 0x0028 25344 // Animation = THIEF_MALE_HUMAN
		WRITE_BYTE 0x002c 226 // Metal color = Metal normal - Eigengrau
		WRITE_BYTE 0x002d 242 // Minor color = Thief Minor - Pale Olive
		WRITE_BYTE 0x002e 241 // Major color = Thief Major - Olive
		WRITE_BYTE 0x0030 228 // Leather color = Leather - Spectral Green
		WRITE_BYTE 0x0031 244 // Armor color = Fighter Minor - Dark Cadet Gray
		WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
		WRITE_ASCII SCRIPT_GENERAL ~~ #8
		WRITE_ASCII SCRIPT_RACE ~~ #8
		WRITE_ASCII SCRIPT_CLASS ~~ #8
		WRITE_ASCII SCRIPT_DEFAULT ~~ #8
		WRITE_ASCII DEATHVAR ~#LFFAss2~ #32
		WRITE_ASCII DIALOG ~~ #8

	COPY_EXISTING ~bdfist24.CRE~ ~override/#LFFAss3.cre~
		SAY NAME1 @1006 /* ~Disguised Flaming Fist Healer~ */
		SAY NAME2 @1006 /* ~Disguised Flaming Fist Healer~ */
		WRITE_LONG 0x0028 25344 // Animation = THIEF_MALE_HUMAN
		WRITE_BYTE 0x002c 226 // Metal color = Metal normal - Eigengrau
		WRITE_BYTE 0x002d 242 // Minor color = Thief Minor - Pale Olive
		WRITE_BYTE 0x002e 241 // Major color = Thief Major - Olive
		WRITE_BYTE 0x0030 228 // Leather color = Leather - Spectral Green
		WRITE_BYTE 0x0031 244 // Armor color = Fighter Minor - Dark Cadet Gray
		WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
		WRITE_ASCII SCRIPT_GENERAL ~~ #8
		WRITE_ASCII SCRIPT_RACE ~~ #8
		WRITE_ASCII SCRIPT_CLASS ~~ #8
		WRITE_ASCII SCRIPT_DEFAULT ~~ #8
		WRITE_ASCII DEATHVAR ~#LFFAss3~ #32
		WRITE_ASCII DIALOG ~~ #8

	// #L_CWBridgeQuest global variable used as a means to track the progress of the quest
	// 0 = not started
	// 1 = warned to be on the look out 3000
	// 2 = Locals have warned about the bridge being held but the PC hasn't seen it yet 3001
	// 3 = PC has gotten close enough to the bridge to see it's held 3002
	// 4 = Corwin has been informed (one way or another) that the bridge is being held by Caelar's forces and she's gathering more intel 3003-5 
	// 5 = Using disguises to disarm the explosives on the bridge 3008,10 without or with password
	// 6 = Using PC as bait to disarm the explosives on the bridge 3009,11 without or with password
	// 97 = PC took on the bridge without the Flaming Fist 3012
	// 98 = Flaming Fist botched the password 3013
	// 99 = The charmed informant escaped and ruined the plan 3014
	
	// #L_CWBridgeRigged global variable used to track if PC knows the bridge is rigged with explosives 3006-7
	// #L_CWBridgeExplosivesTalk global variable used to track if PC and Corwin have discussed the explosives
	// #L_CWBridgePassword global variable used to track if FF know the password to gain access to the bridge
	// #L_CWBridgeWarn global variable used to track how often the PC is warned by locals that bridge is held by Caelar..0 1 or 2

   	// Define the journal entries to be used during the quest
	ADD_JOURNAL @3000 @3001 @3002 @3003 @3004 @3005 @3006 @3007 @3008 @3009 @3010 @3011 @3012 @3013 @3014 USING ~%tra_loc%/%s/sod_coastway_crossing.tra~

	// Compile the area scripts
	EXTEND_TOP ~BD1000.BCS~ ~%mod_root%/scripts/sod_coastway_crossing_bd1000.baf~ USING ~%tra_loc%/%s/sod_coastway_crossing.tra~
	EXTEND_TOP ~BD1200.BCS~ ~%mod_root%/scripts/sod_coastway_crossing_bd1200.baf~ USING ~%tra_loc%/%s/sod_coastway_crossing.tra~

	// Compile dialogues
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/sod_coastway_crossing.d~ USING ~%tra_loc%/%s/sod_coastway_crossing.tra~

	// New scrying pool cutscene
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LCWBQ01.baf~ USING ~%tra_loc%/%s/sod_coastway_crossing.tra~
	
	// Bridge infiltration cutscenes
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LCWBQ02.baf~ USING ~%tra_loc%/%s/sod_coastway_crossing.tra~	// Put FF in place
END
