BACKUP ~weidu_external/backup/sodoff~
SUPPORT ~~
VERSION ~v0.1~

README ~sodoff/readme.sodoff.%LANGUAGE%.txt~ ~sodoff/readme.sodoff.english.txt~

ALWAYS
   ACTION_IF !VARIABLE_IS_SET deja_vu BEGIN
      // Global static variables used by WeiDU
      OUTER_SPRINT mod_root "sodoff"
      OUTER_SPRINT external_loc "weidu_external"
      OUTER_SPRINT tra_base "languages"
      OUTER_SPRINT base_language "american"
      OUTER_SPRINT workspace "%external_loc%/workspace/%mod_root%"
      OUTER_SPRINT tra_loc "%workspace%/%tra_base%"

      // Makes external working directories
      // And copies language files to them
      INCLUDE ~%mod_root%/lib/run_once.tpa~
      LAF DO_RUN_ONCE END

      // Variables used to point to files
      ACTION_IF GAME_IS ~eet~ BEGIN
         INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
      END
      INCLUDE ~%mod_root%/lib/#l_cmpvars.tpa~

      // Library functions used throughout
      INCLUDE ~%mod_root%/lib/alter_script.tpa~
      INCLUDE ~%mod_root%/lib/update_bcs.tpa~
   END

   // This needs to be run before every component for some reason
   // It has something to do with languages that use a different character set
   ACTION_DEFINE_ARRAY thismod_noconvert BEGIN setup END
/////////// DON'T THINK I'LL NEED GAME.TRA //////////////////////////
//   ACTION_DEFINE_ARRAY thismod_reload BEGIN game cases END
////////////////////////////////////////////////////////////////////
   ACTION_DEFINE_ARRAY thismod_reload BEGIN cases END

   LAF HANDLE_CHARSETS 
      INT_VAR 
         infer_charsets = 1 
      STR_VAR 
         iconv_path = EVAL "%mod_root%/%tra_base%/iconv"
         tra_path = EVAL "%tra_loc%"
         noconvert_array = thismod_noconvert
         reload_array = thismod_reload
   END

   ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
      FAIL @1 /* ~Modmerge or Argent's DLC Merger is required before mods can be installed on this game.~ */
   END
   ACTION_IF ((NOT GAME_IS ~eet~) AND (NOT (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))) BEGIN
      FAIL @2 /* ~This mod is requires EET or SoD~ */
   END

   ACTION_IF !VARIABLE_IS_SET deja_vu BEGIN
      // This actually installs changes to a 2da file so want it to run after the game check
      INCLUDE ~%mod_root%/lib/cases.tpa~
      LAF INSTALL_PROPER_CASES END

      OUTER_SET deja_vu = 1
   END
END

// Can't use variables here for some reason
// Not sure why we use both folders, but that's what the example did
/////////// DON'T THINK I'LL NEED GAME.TRA //////////////////////////
//   ~sodoff/languages/american/game.tra~
//   ~weidu_external/workspace/sodoff/languages/american/game.tra~
////////////////////////////////////////////////////////////////////
LANGUAGE ~American English~ ~american~   
   ~sodoff/languages/american/setup.tra~
   ~weidu_external/workspace/sodoff/languages/american/setup.tra~
   ~sodoff/languages/american/cases.tra~
   ~weidu_external/workspace/sodoff/languages/american/cases.tra~


///////////////////////////////////////////////////
// ADD STAT-BASED OBSERVATIONS AND QUEST OPTIONS //
///////////////////////////////////////////////////
BEGIN @4   // MOD 0
LABEL "#L-SOD-STAT_BASED_OPTIONS"
INCLUDE ~sodoff/components/stat_based_sod.tpa~
LAF stat_based_sod END

///////////////////////////////////////////
// ADD SOME SNARK TO STUFFY SOD DIALOGUE //
///////////////////////////////////////////
BEGIN @5   // MOD 10
DESIGNATED 10
LABEL "#L-SOD-SNARK"
INCLUDE ~sodoff/components/snark.tpa~
LAF snark END

///////////////////////
// GIVE AID TO IMOEN //
///////////////////////
BEGIN @8  // MOD 20
DESIGNATED 20
LABEL "#L-SOD-AID_IMOEN"
INCLUDE ~sodoff/components/aid_imoen.tpa~
LAF aid_imoen END

//////////////////
// DITCH CORWIN //
//////////////////
BEGIN @6    // MOD 30
DESIGNATED 30
LABEL "#L-SOD-DITCH_CORWIN"
INCLUDE ~sodoff/components/ditch_corwin.tpa~
LAF ditch_corwin END

//////////////////////////////////
// SCOUT COAST WAY BRIDGE QUEST //
//////////////////////////////////
BEGIN @7    // MOD 40
DESIGNATED 40
LABEL "#L-SOD-COASTWAY_BRIDGE_QUEST"
INCLUDE ~sodoff/components/coastway_bridge.tpa~
LAF coastway_bridge END

