BACKUP ~weidu_external/backup/imoensmagic~
SUPPORT ~Not released yet - Unsupported~
VERSION ~v0.1~

README ~imoensmagic/readme.imoensmagic.%LANGUAGE%.txt~ ~imoensmagic/readme.imoensmagic.english.txt~

ALWAYS
   ACTION_IF !VARIABLE_IS_SET deja_vu BEGIN
      // Global static variables used by WeiDU
      OUTER_SPRINT mod_root "imoensmagic"
      OUTER_SPRINT external_loc "weidu_external"
      OUTER_SPRINT tra_base "languages"
      OUTER_SPRINT base_language "american"
      OUTER_SPRINT workspace "%external_loc%/workspace/%mod_root%"
      OUTER_SPRINT tra_loc "%workspace%/%tra_base%"

      // Makes external working directories
      // And copies language files to them
      INCLUDE ~%mod_root%/lib/run_once.tpa~
      LAF DO_RUN_ONCE END

      // Variables used to point to files and stuff
      INCLUDE ~%mod_root%/lib/#l_cmpvars.tpa~

      // Library functions I use frequently
      INCLUDE ~%mod_root%/lib/alter_script.tpa~
      INCLUDE ~%mod_root%/lib/update_bcs.tpa~
      INCLUDE ~%mod_root%/lib/gw_cartes.tpa~
      INCLUDE ~%mod_root%/lib/change_door_name.tpa~
   END

   // This needs to be run before every component for some reason
   // It has something to do with languages that use a different character set
   ACTION_DEFINE_ARRAY thismod_noconvert BEGIN setup END
   ACTION_DEFINE_ARRAY thismod_reload BEGIN game cases END

   LAF HANDLE_CHARSETS 
      INT_VAR 
         infer_charsets = 1 
      STR_VAR 
         iconv_path = EVAL "%mod_root%/%tra_base%/iconv"
         tra_path = EVAL "%tra_loc%"
         noconvert_array = thismod_noconvert
         reload_array = thismod_reload
   END

   ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
      FAIL @1 /* ~Modmerge or Argent's DLC Merger is required before mods can be installed on this game.~ */
   END

   ACTION_IF !VARIABLE_IS_SET deja_vu BEGIN
      // This actually installs changes to a 2da file so want it to run after the game check
      INCLUDE ~%mod_root%/lib/cases.tpa~
      LAF INSTALL_PROPER_CASES END

      OUTER_SET deja_vu = 1
   END
END

// Can't use variables here for some reason
// Not sure why we use both folders, but that's what the example did
LANGUAGE ~American English~ ~american~   
   ~imoensmagic/languages/american/game.tra~
   ~weidu_external/workspace/imoensmagic/languages/american/game.tra~
   ~imoensmagic/languages/american/setup.tra~
   ~weidu_external/workspace/imoensmagic/languages/american/setup.tra~
   ~imoensmagic/languages/american/cases.tra~
   ~weidu_external/workspace/imoensmagic/languages/american/cases.tra~

///////////////////////////////////////////////
// IMOEN GIVEN SPECIAL WAND OF MAGIC MISSILE //
///////////////////////////////////////////////
BEGIN @4   // MOD 0
LABEL "#L-TRANSITIONS-IMOEN_SOD_WAND"
REQUIRE_PREDICATE GAME_INCLUDES ~sod~ @2
INCLUDE ~imoensmagic/components/imoen_sod_wand.tpa~
LAF imoen_sod_wand END

///////////////////////////////////////
// IMOEN GIVEN MAGIC MISSILE ABILITY //
///////////////////////////////////////
BEGIN @5   // MOD 1
LABEL "#L-TRANSITIONS-IMOEN_BG2_MM"
REQUIRE_PREDICATE (GAME_IS ~bg2ee eet~) @3
INCLUDE ~imoensmagic/components/imoen_bg2_mm.tpa~
LAF imoen_bg2_mm END

